S -> title dramatis_personae actlist
title -> wordlist peq
peq -> PE | QUESTION
dramatis_personae -> ACTOR COMMA wordlist peq dramatis_personae_
dramatis_personae_ -> dramatis_personae | λ

actlist -> act actlist_
actlist_ -> actlist | λ

act -> ACT COLON wordlist peq scenelist

scenelist -> scene scenelist_
scenelist_ -> scenelist | λ
scene -> SCENE COLON wordlist peq statement_list

statement_list -> statement statement_list | λ
statement -> enterexit_stmt | line
line -> ACTOR COLON optional_addressee sentence_list
optional_addressee -> ACTOR COMMA | λ
optional_interjection -> INTERJECTION COMMA | λ
sentence_list -> optional_interjection sentence sentence_list_
sentence_list_ -> λ  | optional_interjection sentence sentence_list_
sentence -> io PE | assignment PE | goto PE | stackop PE | question QUESTION | conditional

io -> SPEAK_YOUR_MIND | OPEN_YOUR_MIND | OPEN_YOUR_HEART | LISTEN_TO_YOUR_HEART

enterexit_stmt  -> LBRACKET enterexit_stmt_
enterexit_stmt_ -> ENTER actorlist RBRACKET
enterexit_stmt_ -> EXIT actorlist RBRACKET
enterexit_stmt_ -> EXEUNT exeunt_
exeunt_ ->  actorlist2 RBRACKET | OMNES RBRACKET | RBRACKET

assignment -> YOU assignment_
assignment_ -> ARE assignment__ | value 
assignment__ -> AS adj AS arithmetic | arithmetic
assignment -> I AM arithmetic

arithmetic -> ARTICLE value | THE value | POSSESSIVE value | YOU | MYSELF | YOURSELF | ACTOR
arithmetic -> NOTHING
arithmetic -> THE_SUM_OF arithmetic AND arithmetic
arithmetic -> THE_DIFFERENCE_BETWEEN arithmetic AND arithmetic
arithmetic -> THE_PRODUCT_OF arithmetic AND arithmetic
arithmetic -> THE_QUOTIENT_OF arithmetic AND arithmetic
arithmetic -> THE_QUOTIENT_BETWEEN arithmetic AND arithmetic
arithmetic -> THE_REMAINDER_OF arithmetic AND arithmetic
arithmetic -> THE_REMAINDER_BETWEEN arithmetic AND arithmetic
arithmetic -> THE_SCR_OF arithmetic
arithmetic -> TWICE_THRICE_HALF arithmetic
arithmetic -> BOTH arithmetic AND arithmetic
arithmetic -> EITHER arithmetic OR arithmetic
arithmetic -> THE_OPPOSITE_OF arithmetic

actorlist -> ACTOR actorlist_
actorlist_ -> λ | COMMA actorlist2 |  AND ACTOR
actorlist2 -> ACTOR actorlist2_
actorlist2_ -> COMMA actorlist2 | AND ACTOR

value -> adj_list noun | noun
noun -> POS_NOUN | NEG_NOUN

noun -> POS_NOUN | NEG_NOUN
adj -> POS_ADJ | NEG_ADJ
adj_list -> adj adj_list_
adj_list_ -> λ | adj_list

let_us -> LET US | WE let_us_
let_us_ -> SHALL | MUST | OUGHT TO
goto -> let_us goto_
goto_ -> RETURN | PROCEED goto__
goto__ -> TO goto_target | POSTHASTE TO goto_target

goto_target -> ACT goto_target_ | SCENE
goto_target_ -> λ | COMMA SCENE 

stackop -> REMEMBER arithmetic
stackop -> RECALL wordlist

question -> AM I comparison
question -> IS arithmetic comparison
question -> ARE YOU comparison

comparison -> BETTER THAN arithmetic
comparison -> MORE adj THAN arithmetic
comparison -> AS adj AS arithmetic
comparison -> WORSE THAN arithmetic
comparison -> LESS adj THAN arithmetic
comparison -> AT_LEAST_AS adj AS arithmetic
comparison -> AT_MOST_AS adj AS arithmetic
comparison -> DIFFERENT FROM arithmetic

conditional -> IF conditional_
conditional_ -> SO COMMA optional_interjection sentence
conditional_ -> NOT COMMA optional_interjection sentence

wordlist -> allwords wordlist | λ
allwords -> ANYWORD | keyword
keyword ->                                  keyword_
keyword_ -> ARTICLE |                       keyword__
keyword__ -> AM  |                          keyword___
keyword___ -> IS  |                         keyword____
keyword____ -> POS_NOUN |                   keyword_____
keyword_____ -> TWICE_THRICE_HALF |         keyword______
keyword______ -> WE |                       keyword_______
keyword_______ -> POSSESSIVE |              keyword________
keyword________ -> ACTOR |                  keyword_________
keyword_________ -> TO |                    keyword__________
keyword__________ -> OPEN_YOUR_HEART |      keyword___________
keyword___________ -> SPEAK_YOUR_MIND |     keyword____________
keyword____________ -> adj |                keyword_____________
keyword_____________ -> AND |               keyword______________
keyword______________ -> THE_SUM_OF |       keyword_______________
keyword_______________ -> LESS |            keyword________________
keyword________________ -> MORE |           keyword_________________
keyword_________________ -> YOU |           keyword__________________
keyword__________________ -> IF |           keyword___________________
keyword___________________ -> NEG_NOUN |    keyword____________________
keyword____________________ -> THE |        keyword_____________________
keyword_____________________ -> LET |       keyword______________________
keyword______________________ -> US |       keyword_______________________
keyword_______________________ -> BOTH |    keyword________________________
keyword________________________ -> EITHER | keyword_________________________
keyword_________________________ -> ARE |   keyword__________________________
keyword__________________________ -> NOT |  keyword___________________________
keyword___________________________ -> EXIT          | keyword____________________________ 
keyword____________________________ -> PROCEED      | keyword_____________________________
keyword_____________________________ -> THAN        | keyword______________________________
keyword______________________________ -> OR         | keyword_______________________________
keyword_______________________________ -> FROM      | keyword________________________________
keyword________________________________ -> I        | keyword_________________________________
keyword_________________________________ -> NOTHING

