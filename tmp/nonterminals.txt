S -> title dramatis-personae actlist
title -> wordlist peq
peq -> PE | QUESTION
dramatis-personae -> ACTOR COMMA wordlist peq dramatis-personae'
dramatis-personae' -> dramatis-personae | λ

actlist -> act actlist'
actlist' -> act | λ

act -> ACT act'
act' -> SOME_ROMAN_NUMBERS COLON wordlist peq scenelist
act' ->  I COLON wordlist peq scenelist

scenelist -> scene scenelist'
scenelist' -> scenelist | λ
scene -> SCENE scene'
scene' -> SOME_ROMAN_NUMBERS COLON wordlist peq statement-list
scene' -> I COLON wordlist peq statement-list

statement-list -> statement statement-list | λ
statement -> enterexit-stmt | line
line -> ACTOR COLON optional-addressee sentence-list
optional-addressee -> ACTOR COMMA | λ
optional-interjection -> INTERJECTION COMMA | λ
sentence-list -> optional-interjection sentence sentence-list'
sentence-list' -> λ  | sentence sentence-list'
sentence -> io PE | assignment PE | goto PE | stackop PE | question QUESTION | conditional

io -> SPEAK_YOUR_MIND | OPEN_YOUR_MIND | OPEN_YOUR_HEART | LISTEN_TO_YOUR_HEART

enterexit-stmt  -> LBRACKET enterexit-stmt'
enterexit-stmt' -> ENTER actorlist RBRACKET
enterexit-stmt' -> EXIT actorlist RBRACKET
enterexit-stmt' -> EXEUNT exeunt'
exeunt' ->  actorlist RBRACKET | OMNES RBRACKET | RBRACKET

assignment -> YOU assignment'
assignment' -> ARE assignment'' | value
assignment'' -> AS adj AS arithmetic | arithmetic
assignment -> I AM arithmetic

arithmetic -> ARTICLE value | THE value | POSSESSIVE value | YOU | MYSELF | YOURSELF | ACTOR
arithmetic -> NOTHING
arithmetic -> THE_SUM_OF arithmetic AND arithmetic
arithmetic -> THE_DIFFERENCE_BETWEEN arithmetic AND arithmetic
arithmetic -> THE_PRODUCT_OF arithmetic AND arithmetic
arithmetic -> THE_QUOTIENT_OF arithmetic AND arithmetic
arithmetic -> THE_QUOTIENT_BETWEEN arithmetic AND arithmetic
arithmetic -> THE_REMAINDER_OF arithmetic AND arithmetic
arithmetic -> THE_REMAINDER_BETWEEN arithmetic AND arithmetic
arithmetic -> THE_SCR_OF arithmetic
arithmetic -> TWICE_THRICE_HALF arithmetic
arithmetic -> BOTH arithmetic AND arithmetic
arithmetic -> EITHER arithmetic OR arithmetic
arithmetic -> THE_OPPOSITE_OF arithmetic

value -> adj-list noun | noun

actorlist -> ACTOR actorlist'
actorlist' -> λ | COMMA actorlist2 |  AND ACTOR
actorlist2 -> ACTOR actorlist2'
actorlist2' -> COMMA actorlist2 | AND ACTOR

noun -> POS_NOUN | NEG_NOUN
adj -> POS_ADJ | NEG_ADJ
adj-list -> adj adj-list'
adj-list' -> λ | adj


let_us -> LET US | WE let_us'
let_us' -> SHALL | MUST | OUGHT TO
goto -> let_us goto'
goto' -> RETURN | PROCEED goto''
goto'' -> TO goto-target | POSTHASTE TO goto-target

goto-target -> ACT goto-target' | SCENE goto-target''
goto-target' -> SOME_ROMAN_NUMBERS goto-target''''
goto-target' ->  I goto-target''''
goto-target'''' -> COMMA SCENE goto-target''' | λ
goto-target''' -> SOME_ROMAN_NUMBERS | I
goto-target'' -> I
goto-target'' -> SOME_ROMAN_NUMBERS

stackop -> REMEMBER arithmetic
stackop -> RECALL wordlist

question -> AM I comparison
question -> IS arithmetic comparison
question -> ARE YOU comparison

comparison -> BETTER THAN arithmetic
comparison -> MORE adj THAN arithmetic
comparison -> AS adj AS arithmetic
comparison -> WORSE THAN arithmetic
comparison -> LESS adj THAN arithmetic
comparison -> AT_LEAST_AS adj AS arithmetic
comparison -> AT_MOST_AS adj AS arithmetic
comparison -> DIFFERENT FROM arithmetic

conditional -> IF conditional'
conditional' -> SO COMMA optional-interjection sentence
conditional' -> NOT COMMA optional-interjection sentence

wordlist -> ARTICLE | AM | I | noun | adj | THE | WE | ACTOR | AND | OF | SUM | IS | LESS | MORE | YOU | IF | IS | POSSESSIVE | TO | OPEN_YOUR_HEART | SPEAK_YOUR_MIND | ANYWORD | wordlist'
wordlist' -> λ | wordlist
