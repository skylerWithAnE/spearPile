S -> title dramatis-personae actlist

title -> wordlist peq
peq -> PERIOD | QUESTION | EXCLAMATION
pe -> PERIOD | EXCLAMATION
dramatis-personae -> ACTOR COMMA wordlist peq dramatis-personae
dramatis-personae -> ACTOR COMMA wordlist peq

actlist -> act actlist | act

act -> ACT  COLON wordlist peq scenelist
act -> ACT I COLON wordlist peq scenelist

scenelist -> scene scenelist | scene
scene -> SCENE  COLON wordlist peq statement-list
scene -> SCENE I COLON wordlist peq statement-list

statement-list -> statement statement-list
statement -> enter-stmt | exit-stmt | line
line -> ACTOR COLON optional-addressee sentence-list | ACTOR COLON sentence-list
optional-addressee -> ACTOR COMMA
optional-interjection -> interjection COMMA
interjection -> NAY | YEA | VERILY | FORSOOTH | PERADVENTURE | PERCHANCE
sentence-list -> optional-interjection sentence sentence-list | optional-interjection sentence | sentence sentence-list | sentence
sentence -> io pe | assignment pe | goto pe | stackop pe | question QUESTION | conditional

io -> SPEAK_YOUR_MIND | OPEN_YOUR_MIND | OPEN_YOUR_HEART | LISTEN_TO_YOUR_HEART

enter-stmt -> LBRACKET ENTER ACTOR RBRACKET | LBRACKET ENTER actorlist RBRACKET
exit-stmt -> LBRACKET EXIT ACTOR RBRACKET
exit-stmt -> LBRACKET EXEUNT actorlist RBRACKET
exit-stmt -> LBRACKET EXEUNT OMNES RBRACKET
exit-stmt -> LBRACKET EXEUNT RBRACKET

assignment -> YOU ARE AS adj AS arithmetic
assignment -> YOU ARE arithmetic
assignment -> YOU value
assignment -> I AM arithmetic

arithmetic -> ARTICLE value | THE value | POSSESSIVE value | YOU | MYSELF | YOURSELF | ACTOR
arithmetic -> NOTHING
arithmetic -> THE SUM OF arithmetic AND arithmetic
arithmetic -> THE DIFFERENCE BETWEEN arithmetic AND arithmetic
arithmetic -> THE PRODUCT OF arithmetic AND arithmetic
arithmetic -> THE QUOTIENT OF arithmetic AND arithmetic
arithmetic -> THE QUOTIENT BETWEEN arithmetic AND arithmetic
arithmetic -> THE REMAINDER OF arithmetic AND arithmetic
arithmetic -> THE REMAINDER BETWEEN arithmetic AND arithmetic
arithmetic -> THE SQUARE OF arithmetic
arithmetic -> THE CUBE OF arithmetic
arithmetic -> THE SQUARE ROOT OF arithmetic
arithmetic -> TWICE arithmetic
arithmetic -> THRICE arithmetic
arithmetic -> HALF arithmetic
arithmetic -> BOTH arithmetic AND arithmetic
arithmetic -> EITHER arithmetic OR arithmetic
arithmetic -> THE OPPOSITE OF arithmetic

value -> adj-list noun | noun

actorlist -> ACTOR COMMA actorlist | ACTOR AND ACTOR
noun -> POS_NOUN | NEG_NOUN
adj -> POS_ADJ | NEG_ADJ
adj-list -> adj adj-list | adj


let_us -> LET US | WE SHALL | WE MUST | WE OUGHT TO
goto -> let_us PROCEED TO goto-target
goto -> let_us PROCEED POSTHASTE TO goto-target
goto -> let_us RETURN

goto-target -> ACT COMMA SCENE
goto-target -> ACT 
goto-target -> wordlist

stackop -> REMEMBER arithmetic
stackop -> RECALL wordlist

question -> AM I comparison
question -> IS arithmetic comparison
question -> ARE YOU comparison

comparison -> BETTER THAN arithmetic
comparison -> MORE adj THAN arithmetic
comparison -> AS adj AS arithmetic
comparison -> WORSE THAN arithmetic
comparison -> LESS adj THAN arithmetic
comparison -> AT LEAST AS adj AS arithmetic
comparison -> AT MOST AS adj AS arithmetic
comparison -> DIFFERENT FROM arithmetic

conditional -> IF SO COMMA optional-interjection sentence | IF SO COMMA sentence
conditional -> IF NOT COMMA optional-interjection sentence | IF NOT COMMA sentence

wordlist -> allwords | allwords wordlist
allwords -> WORDS | ARTICLE | AM | I | noun | adj | THE | WE | ACTOR | AND | OF | SUM | IS | LESS | MORE | YOU | IF | IS | POSSESSIVE | TO | OPEN_YOUR_HEART | SPEAK_YOUR_MIND

